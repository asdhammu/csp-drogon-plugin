{
    parserClass="com.asdhammu.cspdrogon.language.parser.CSPDrogonParser"
    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="CSPDrogon"
    psiImplClassSuffix="Impl"
    psiPackage="com.asdhammu.cspdrogon.language.psi"
    psiImplPackage="com.asdhammu.cspdrogon.language.psi.impl"

    elementTypeHolderClass="com.asdhammu.cspdrogon.language.psi.CSPDrogonTypes"
    elementTypeClass="com.asdhammu.cspdrogon.language.psi.CSPDrogonElementType"
    tokenTypeClass="com.asdhammu.cspdrogon.language.psi.CSPDrogonTokenType"

    psiImplUtilClass="com.asdhammu.cspdrogon.language.psi.impl.CSPDrogonPsiImplUtil"
}

CSPDirectiveFile ::= (item_)*

// Update item_ to include template data
private item_ ::= (csp_directive | TEMPLATE_DATA | OUTER_ELEMENT_TYPE)

csp_directive ::= (view_directive | layout_directive | param_directive | cpp_directive | cpp_inc_directive) {
pin=1
}

cpp_inc_directive ::= CPP_INCLUDE_START CPP_INCLUDE cpp_header_file CPP_INCLUDE_END {
pin=1
}

cpp_header_file ::= H_FILE_START_DELIMITER H_FILE H_FILE_EXT H_FILE_END_DELIMITER

// Simplified cpp_directive
cpp_directive ::= CPP_VIEW_START cpp_content_block CPP_VIEW_END {
    pin=1
}

// Just a container for the injector to target
cpp_content_block ::= CPP_CONTENT_TOKEN{
    mixin="com.asdhammu.cspdrogon.language.psi.impl.CSPDrogonInjectionHostImpl"
}

view_directive ::= VIEW_START directive_content {
pin=1
methods=[getDirectiveType]
}

layout_directive ::= LAYOUT_START directive_content{
pin=1
methods=[getDirectiveType]
}

private directive_content ::= FILE_NAME DIRECTIVE_END{
pin=1
}

param_directive ::= PARAM_START param_directive_content{
    pin = 1
}

private param_directive_content ::= PARAM_VARIABLE_NAME* PARAM_END{
pin=1
}