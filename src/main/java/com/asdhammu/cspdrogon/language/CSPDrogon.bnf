{
    parserClass="com.asdhammu.cspdrogon.language.parser.CSPDrogonParser"
    extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

    psiClassPrefix="CSPDrogon"
    psiImplClassSuffix="Impl"
    psiPackage="com.asdhammu.cspdrogon.language.psi"
    psiImplPackage="com.asdhammu.cspdrogon.language.psi.impl"

    elementTypeHolderClass="com.asdhammu.cspdrogon.language.psi.CSPDrogonTypes"
    elementTypeClass="com.asdhammu.cspdrogon.language.psi.CSPDrogonElementType"
    tokenTypeClass="com.asdhammu.cspdrogon.language.psi.CSPDrogonTokenType"

    psiImplUtilClass="com.asdhammu.cspdrogon.language.psi.impl.CSPDrogonPsiImplUtil"
}

CSPDirectiveFile ::= (item_)*

private item_ ::= (html_element | doctype)

doctype ::= XML_DOCTYPE_START doctype_content* XML_DOCTYPE_END

doctype_content ::= XML_NAME | XML_DOCTYPE_PUBLIC | XML_ATTRIBUTE_VALUE_TOKEN

html_element ::= empty_element | start_tag_element | csp_directive

start_tag_element ::= startTag content endTag
empty_element ::= startEmptyTag XML_EMPTY_ELEMENT_END

private startTag ::= XML_START_TAG_START XML_NAME attribute* XML_TAG_END
private endTag ::= XML_END_TAG_START XML_NAME XML_TAG_END
private startEmptyTag ::= XML_START_TAG_START XML_NAME attribute*

private attribute ::= XML_NAME XML_EQ attributeValue
private attributeValue ::= XML_ATTRIBUTE_VALUE_START_DELIMITER XML_ATTRIBUTE_VALUE_TOKEN* XML_ATTRIBUTE_VALUE_END_DELIMITER | XML_ATTRIBUTE_VALUE_TOKEN

private content ::= ( html_element
| XML_DATA_CHARACTERS
| XML_CHAR_ENTITY_REF
| XML_TAG_CHARACTERS
| XML_ENTITY_REF_TOKEN
| WHITE_SPACE
)*

csp_directive ::= (view_directive | layout_directive | param_directive | cpp_directive | cpp_inc_directive) {
pin=1
}

cpp_inc_directive ::= CPP_INCLUDE_START CPP_INCLUDE cpp_header_file CPP_INCLUDE_END {
pin=1
}

cpp_header_file ::= H_FILE_START_DELIMITER H_FILE H_FILE_EXT H_FILE_END_DELIMITER

cpp_directive ::= CPP_VIEW_START cpp_content CPP_VIEW_END {
pin=1
}

cpp_content ::= cpp_variable_content | (cpp_controller_variables)*

cpp_variable_content ::= DOLLARSIGN CPP_VARIABLE_NAME SEMICOLON

cpp_controller_variables ::= cpp_left_side CPP_EQ cpp_right_side

private cpp_left_side ::= CPP_TYPE CPP_VARIABLE_NAME
private cpp_right_side ::= CPP_AT_THE CPP_DOT CPP_GET BRACKET_START CPP_TYPE BRACKET_END ROUND_BRACKET_START DELI_START CPP_VARIABLE_NAME DELI_END ROUND_BRACKET_END SEMICOLON

view_directive ::= VIEW_START directive_content {
pin=1
methods=[getDirectiveType]
}

layout_directive ::= LAYOUT_START directive_content{
pin=1
methods=[getDirectiveType]
}

private directive_content ::= WHITE_SPACE* file_reference WHITE_SPACE* DIRECTIVE_END{
pin=2
}

file_reference ::= FILE_NAME CSP_EXT {
pin=1
recoverWhile=file_reference_recover
}

private file_reference_recover ::= !(DIRECTIVE_END | WHITE_SPACE)

param_directive ::= PARAM_START param_directive_content{
    pin = 1
}

private param_directive_content ::= WHITE_SPACE* PARAM_VARIABLE_NAME* WHITE_SPACE* PARAM_END{
pin=2
}